buildscript {
    repositories {
        google {
            content {
                includeGroupByRegex "com\\.android.*"
                includeGroupByRegex "androidx.*"
                includeGroupByRegex "android.arch.*"
                includeGroupByRegex "com\\.google.*"
            }
        }
        mavenCentral()
        gradlePluginPortal()
        if (project.hasProperty("useMavenLocal")) {
            mavenLocal()
        }
    }
    dependencies {
        classpath "com.project.starter:android:0.88.0"
        classpath "io.github.usefulness:screenshot-testing-plugin:1.2.1"
        if (project.hasProperty("useMavenLocal")) {
            classpath "com.project.starter.local:easylauncher:+"
        } else {
            classpath "com.project.starter:easylauncher"
        }
        def agpVersion = project.properties["agpVersion"]?.toString()
        logger.quiet("agpVersion=$agpVersion, gradle=${gradle.gradleVersion}")
        if (agpVersion != null) {
            classpath("com.android.tools.build:gradle") {
                version {
                    strictly(agpVersion)
                }
            }
        }
    }
}

apply plugin: "com.starter.config"

commonConfig {
    androidPlugin {
        minSdkVersion 23
    }
}

def agpVersion = project.properties["agpVersion"] ?: ""
subprojects {
    pluginManager.withPlugin("com.android.application") {
        pluginManager.apply("io.github.usefulness.screenshot-testing-plugin")

        android {
            buildFeatures {
                buildConfig = true
            }
            testOptions.unitTests.includeAndroidResources true

            packagingOptions {
                exclude "META-INF/LICENSE*"
            }
            applicationVariants.configureEach { variant ->
                variant.mergedFlavor.applicationId = "com.${project.name.replace("-", ".")}.${variant.name}"
                variant.mergedFlavor.manifestPlaceholders += [appName: "$variant.name"]
            }
            buildTypes.configureEach {
                matchingFallbacks = ["debug"]
            }

            lintOptions {
                disable("VectorPath", "HardcodedText", "IconDuplicates", "ContentDescription")

                if (agpVersion == "" || agpVersion.startsWith("8")) {
                    disable("MonochromeLauncherIcon")
                }

                warningsAsErrors = true
                checkReleaseBuilds = false
            }
        }

        tasks.register("recordAll", Copy) {
            group = 'screenshot test'
            def tempDir = layout.buildDirectory.file("screenshots_tmp/")
            from tempDir
            into "screenshots/"

            doLast {
                tempDir.get().asFile.deleteDir()
            }
        }

        String lastTask = null
        android.applicationVariants.configureEach { variant ->
            def recordTask = tasks.findByName("record${variant.name.capitalize()}AndroidTestScreenshotTest")

            if (recordTask != null) {
                def recordAndCopyTask = "recordAndCopy${variant.name.capitalize()}AndroidTestScreenshotTest"

                tasks.register(recordAndCopyTask, Copy) {
                    from "screenshots/"
                    into layout.buildDirectory.file("screenshots_tmp")
                    dependsOn recordTask
                }

                tasks.recordAll.dependsOn recordAndCopyTask
            }

            if (lastTask != null) {
                if (gradle.gradleVersion.startsWith("8.2")) {
                    return
                }
                tasks.named { it == "record${variant.name.capitalize()}AndroidTestScreenshotTest" }.configureEach {
                    mustRunAfter("record${lastTask.capitalize()}AndroidTestScreenshotTest")
                    mustRunAfter("recordAndCopy${lastTask.capitalize()}AndroidTestScreenshotTest")
                }
            }
            lastTask = variant.name
        }

        tasks.register("verifyAll") {
            group = 'screenshot test'
            android.applicationVariants.configureEach { variant ->
                dependsOn("verify${variant.flavorName.capitalize()}DebugAndroidTestScreenshotTest")
            }
        }

        tasks.register("installAll") {
            group = 'install'
            android.applicationVariants.configureEach { variant ->
                dependsOn("install${variant.name.capitalize()}")
            }
        }

        tasks.withType(Test).configureEach {
            useJUnitPlatform()
        }

        dependencies {
            androidTestImplementation project(":screenshot-test-helpers")
        }
    }

    pluginManager.withPlugin("com.android.library") {
        pluginManager.withPlugin("io.github.usefulness.screenshot-testing-plugin") {
            tasks.register("recordAll", Copy) {
                group = 'screenshot test'

                def tempDir = layout.buildDirectory.map { it.dir("screenshots_tmp") }
                from tempDir
                into "screenshots/"

                doLast {
                    tempDir.get().asFile.deleteDir()
                }
            }

            android.libraryVariants.configureEach { variant ->
                def recordTask = tasks.findByName("record${variant.name.capitalize()}AndroidTestScreenshotTest")

                if (recordTask != null) {
                    def recordAndCopyTask = "recordAndCopy${variant.name.capitalize()}AndroidTestScreenshotTest"

                    tasks.register(recordAndCopyTask, Copy) {
                        from "screenshots/"
                        into layout.buildDirectory.dir("screenshots_tmp")
                        dependsOn recordTask
                    }

                    tasks.recordAll.dependsOn recordAndCopyTask
                }
            }

            tasks.register("verifyAll") {
                group = 'screenshot test'
                android.libraryVariants.configureEach { variant ->
                    dependsOn("verify${variant.flavorName.capitalize()}DebugAndroidTestScreenshotTest")
                }
            }

            tasks.withType(Test).configureEach {
                useJUnitPlatform()
            }

            dependencies {
                androidTestImplementation project(":screenshot-test-helpers")
            }
        }
    }

    pluginManager.withPlugin("kotlin") {
        kotlin {
            jvmToolchain(JavaVersion.current().majorVersion.toInteger())
        }
    }

    pluginManager.withPlugin("io.github.usefulness.screenshot-testing-plugin") {
        androidComponents.finalizeDsl { extension ->
            extension.defaultConfig.testInstrumentationRunner = "io.github.usefulness.testing.screenshot.DefaultScreenshotRunner"
        }
    }
}
