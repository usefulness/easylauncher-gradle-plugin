import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "com.starter.config"
    id "com.starter.versioning"
}

commonConfig {
    javaFilesAllowed false
}

allprojects {
    pluginManager.withPlugin("kotlin") { plugin ->
        def kotlinVersion = plugins.getPlugin("kotlin").kotlinPluginVersion
        configurations.configureEach {
            resolutionStrategy.eachDependency {
                if (requested.group == 'org.jetbrains.kotlin' && requested.name.startsWith("kotlin")) {
                    useVersion kotlinVersion
                }
            }
        }
    }
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            useIR = true
        }
    }

    pluginManager.withPlugin("java") {
        java {
            toolchain {
                languageVersion.set(JavaLanguageVersion.of(8))
            }
        }
    }
}
