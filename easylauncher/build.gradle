plugins {
    id "java-gradle-plugin"
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'com.starter.library.kotlin'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.8'
    id 'com.starter.publishing'
}

repositories {
    mavenCentral()
    gradlePluginPortal()
    google()
}

java {
    withSourcesJar()
}

tasks.named("test") {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
}

pluginBundle {
    website = 'https://github.com/mateuszkwiecinski/easylauncher-gradle-plugin/'
    vcsUrl = 'https://github.com/mateuszkwiecinski/easylauncher-gradle-plugin.git'
    description = 'Set of plugins that might be useful for Multi-Module Android projects.'
    tags = ['android', 'launcher', "flavor", "buildType"]
}

gradlePlugin {
    plugins {
        quality {
            id = 'com.starter.easylauncher'
            displayName = 'Automatically customizes Android laucher icon per each build variant'
            implementationClass = 'com.project.starter.easylauncher.plugin.EasyLauncherPlugin'
        }
    }
}

tasks.withType(PluginUnderTestMetadata).configureEach {
    pluginClasspath.from(configurations.compileOnly)
}

dependencies {
    implementation "org.codehaus.groovy:groovy-xml:${GroovySystem.getVersion()}"
    compileOnly "com.android.tools.build:gradle:4.2.2"

    testImplementation gradleTestKit()
    testImplementation "org.assertj:assertj-core:3.20.2"
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.2"
}
